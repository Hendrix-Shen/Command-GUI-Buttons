import groovy.json.JsonSlurper

pluginManagement {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven {
            name("Fabric")
            url("https://maven.fabricmc.net/")
        }
        maven {
            name("Jitpack")
            url("https://jitpack.io")
        }
        maven {
            name("Nyan Maven")
            url("https://github.com/Hendrix-Shen/Maven/raw/master/")
        }
    }
}

def settings = new JsonSlurper().parseText(file("settings.json").text)

for (String version : settings.versions) {
    include(":$version")
    ProjectDescriptor proj = project(":$version")
    proj.setProjectDir(file("versions/$version"))
    proj.setBuildFileName("../${settings.versions.get(0)}/build.gradle")
}

include(":fabricWrapper")
