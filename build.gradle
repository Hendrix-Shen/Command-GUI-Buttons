plugins {
    id("maven-publish")
    id("fabric-loom").version("1.0-SNAPSHOT").apply(false)
    // https://github.com/ReplayMod/preprocessor
    // https://github.com/Fallen-Breath/preprocessor
    id("com.replaymod.preprocess").version("897c4096e2")
    id("org.ajoberstar.grgit").version("5.0.0")
    id("net.kyori.blossom").version("1.3.1").apply(false)
}

preprocess {
    def mc1165 = createNode("1.16.5", 1_16_05, "mojang")
    def mc1171 = createNode("1.17.1", 1_17_01, "mojang")
    def mc1182 = createNode("1.18.2", 1_18_02, "mojang")
    def mc1190 = createNode("1.19",   1_19_00, "mojang")
    def mc1192 = createNode("1.19.2", 1_19_02, "mojang")
    def mc1193 = createNode("1.19.3", 1_19_03, "mojang")

    mc1165.link(mc1171, null)
    mc1171.link(mc1182, null)
    mc1182.link(mc1190, null)
    mc1190.link(mc1192, null)
    mc1192.link(mc1193, null)
}

def ENV = System.getenv()
String realVersion = "${project.mod_version}"
realVersion += "." + (ENV.BUILD_NUMBER ? "${ENV.BUILD_NUMBER}+" : "${new Date().format("yyyyMMdd.HHmmss")}${(grgit.status().clean ? "+" : "-dirty.")}")
realVersion += grgit.head().abbreviatedId

setVersion(realVersion)

task cleanPreprocessSources {
    doFirst {
        subprojects {
            def path = project.projectDir.toPath().resolve('build/preprocessed')
            path.toFile().deleteDir()
        }
    }
}