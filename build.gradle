plugins {
    id("maven-publish")
    id("fabric-loom").version("1.0-SNAPSHOT").apply(false)
    // https://github.com/ReplayMod/preprocessor
    // https://github.com/Fallen-Breath/preprocessor
    id("com.replaymod.preprocess").version("897c4096e2")
    id("org.ajoberstar.grgit").version("5.0.0")
}

preprocess {
    def mc1182 = createNode("1.18.2", 1_18_02, "mojang")
    def mc1190 = createNode("1.19",   1_19_00, "mojang")
    def mc1192 = createNode("1.19.2", 1_19_02, "mojang")
    def mc1193 = createNode("1.19.3", 1_19_03, "mojang")

    mc1182.link(mc1190, null)
    mc1190.link(mc1192, null)
    mc1192.link(mc1193, null)
}

def ENV = System.getenv()
String realVersion = "${project.mod_version}"
realVersion += "." + (ENV.BUILD_NUMBER ? "${ENV.BUILD_NUMBER}" : "${new Date().format("yyyyMMdd.HHmmss")}")
realVersion += (grgit.status().clean ? "+" : "-dirty.")
realVersion += grgit.head().abbreviatedId

setVersion(realVersion)

task cleanPreprocessSources {
    doFirst {
        subprojects {
            def path = project.projectDir.toPath().resolve('build/preprocessed')
            path.toFile().deleteDir()
        }
    }
}